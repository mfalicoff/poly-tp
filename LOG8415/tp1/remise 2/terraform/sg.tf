resource "aws_security_group" "alb" {
  name        = "flask-alb"
  description = "Allow inbound HTTP traffic from the broad internet"
  vpc_id      = data.aws_vpc.default.id
}

# Allow HTTP traffic from the broad internet to reach the ALB
resource "aws_security_group_rule" "alb_ingress" {
  type              = "ingress"
  from_port         = 80
  to_port           = 80
  protocol          = "tcp"
  cidr_blocks       = ["0.0.0.0/0"]
  ipv6_cidr_blocks  = ["::/0"]
  security_group_id = aws_security_group.alb.id
}

# Allow HTTP traffic from the load balancer to EC2 instances
resource "aws_security_group_rule" "alb_egress" {
  type                     = "egress"
  from_port                = 80
  to_port                  = 80
  protocol                 = "tcp"
  source_security_group_id = aws_security_group.ec2.id
  security_group_id        = aws_security_group.alb.id
}

resource "aws_security_group" "ec2" {
  name        = "flask-ec2"
  description = "Allow inbound HTTP traffic from the application load balancer and egress"
  vpc_id      = data.aws_vpc.default.id
}

# Only allow HTTP traffic from the load balancer
resource "aws_security_group_rule" "ec2_ingress" {
  type                     = "ingress"
  from_port                = 80
  to_port                  = 80
  protocol                 = "tcp"
  source_security_group_id = aws_security_group.alb.id
  security_group_id        = aws_security_group.ec2.id
}

# Only all egress traffic from the EC2 to the broad internet
resource "aws_security_group_rule" "ec2_egress" {
  type              = "egress"
  from_port         = 0
  to_port           = 0
  protocol          = "-1"
  cidr_blocks       = ["0.0.0.0/0"]
  ipv6_cidr_blocks  = ["::/0"]
  security_group_id = aws_security_group.ec2.id
}
